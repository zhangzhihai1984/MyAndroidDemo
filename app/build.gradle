apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

buildscript {
    ext.versions = [
            'retrofit2'  : '2.6.2',
            'autodispose': '1.4.0',
            'butterknife': '10.2.0',
            'rxbinding'  : '3.0.0',
    ]
}

android {
    compileSdkVersion 29
    flavorDimensions "default"

    defaultConfig {
        applicationId "com.usher.demo"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        resValue "string", "app_name", "MyDemo"
        buildConfigField "String", "API_HOST", '"https://api.twigcodes.usher"'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

//    configurations.all {
//        resolutionStrategy {
//            eachDependency { details ->
//                // Force all of the primary support libraries to use the same version.
//                if (details.requested.group == 'com.android.support') {
//                    details.useVersion versions.supportLibrary
//                }
//            }
//        }
//    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }

    productFlavors {
        usher {
            applicationId "com.usher.demo"
            ext {
                api_host = [
                        debug  : '"https://api.twigcodes.usher:debug"',
                        local  : '"https://api.twigcodes.usher:local"',
                        release: '"https://api.twigcodes.usher:release"'
                ]
                app_name = [
                        debug  : "Usher-D",
                        local  : "Usher-L",
                        release: "Usher"
                ]
            }
        }
        john {
            applicationId "com.john.demo"
            ext {
                api_host = [
                        debug  : '"https://api.twigcodes.john:debug"',
                        local  : '"https://api.twigcodes.john:local"',
                        release: '"https://api.twigcodes.john:release"'
                ]
                app_name = [
                        debug  : "John-D",
                        local  : "John-L",
                        release: "John"
                ]
            }
        }
    }

    applicationVariants.all { variant ->
        def flavor = variant.productFlavors[0]
        def buildType = variant.buildType.name

        def appName = flavor.app_name[buildType]

        variant.resValue "string", "app_name", appName
        variant.buildConfigField "String", "API_HOST", flavor.api_host[buildType]

        variant.outputs.all { output ->
            outputFileName = "${appName}-${variant.buildType.name}-${variant.versionName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':TwigCodesUI')

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0-beta01'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit2}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit2}"
    implementation "com.squareup.retrofit2:adapter-rxjava:${versions.retrofit2}"
    implementation 'io.reactivex.rxjava2:rxjava:2.2.12'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "com.uber.autodispose:autodispose:${versions.autodispose}"
    implementation "com.uber.autodispose:autodispose-android:${versions.autodispose}"
    implementation "com.uber.autodispose:autodispose-lifecycle:${versions.autodispose}"
    implementation "com.uber.autodispose:autodispose-android-archcomponents:${versions.autodispose}"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.github.ybq:Android-SpinKit:1.1.0'

    implementation "com.jakewharton.rxbinding3:rxbinding:${versions.rxbinding}"
    implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:${versions.rxbinding}"
    implementation "com.jakewharton.rxbinding3:rxbinding-leanback:${versions.rxbinding}"
    implementation "com.jakewharton.rxbinding3:rxbinding-recyclerview:${versions.rxbinding}"
    implementation "com.jakewharton.rxbinding3:rxbinding-swiperefreshlayout:${versions.rxbinding}"
    implementation "com.jakewharton.rxbinding3:rxbinding-viewpager:${versions.rxbinding}"
    implementation "com.jakewharton.rxbinding3:rxbinding-material:${versions.rxbinding}"

    implementation "com.jakewharton:butterknife:${versions.butterknife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${versions.butterknife}"

    implementation 'com.ogaclejapan.smarttablayout:library:2.0.0@aar'
    //Optional: see how to use the utility.
    implementation 'com.ogaclejapan.smarttablayout:utils-v4:2.0.0@aar'

    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-andx-11'
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0-andx-11'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //RecyclerView
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.45'

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.50"
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
}
