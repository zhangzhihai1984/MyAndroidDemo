apply plugin: 'com.android.application'

buildscript {
    ext.versions = [
            'supportLibrary': '1.0.0-beta01',
            'retrofit2'     : '2.4.0',
            'autodispose'   : '1.0.0-RC1'
    ]
}

android {
    compileSdkVersion 28
    flavorDimensions "default"

    defaultConfig {
        applicationId "com.usher.demo"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        resValue "string", "app_name", "MyDemo"
        buildConfigField "String", "API_HOST", '"https://api.twigcodes.usher"'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                // Force all of the primary support libraries to use the same version.
                if (details.requested.group == 'com.android.support') {
                    details.useVersion versions.supportLibrary
                }
            }
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }

    productFlavors {
        usher {
            applicationId "com.usher.demo"
            ext {
                api_host = [
                        debug  : '"https://api.twigcodes.usher:debug"',
                        local  : '"https://api.twigcodes.usher:local"',
                        release: '"https://api.twigcodes.usher:release"'
                ]
                app_name = [
                        debug  : "Usher-D",
                        local  : "Usher-L",
                        release: "Usher"
                ]
            }
        }
        john {
            applicationId "com.john.demo"
            ext {
                api_host = [
                        debug  : '"https://api.twigcodes.john:debug"',
                        local  : '"https://api.twigcodes.john:local"',
                        release: '"https://api.twigcodes.john:release"'
                ]
                app_name = [
                        debug  : "John-D",
                        local  : "John-L",
                        release: "John"
                ]
            }
        }
    }

    applicationVariants.all { variant ->
        def flavor = variant.productFlavors[0]
        def buildType = variant.buildType.name

        def appName = flavor.app_name[buildType]

        variant.resValue "string", "app_name", appName
        variant.buildConfigField "String", "API_HOST", flavor.api_host[buildType]

        variant.outputs.all { output ->
            outputFileName = "${appName}-${variant.buildType.name}-${variant.versionName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:${versions.supportLibrary}"
    implementation "androidx.legacy:legacy-support-v4:$versions.supportLibrary"
    implementation "com.google.android.material:material:${versions.supportLibrary}"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit2}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit2}"
    implementation "com.squareup.retrofit2:adapter-rxjava:${versions.retrofit2}"
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle:2.2.2'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.2'
    //    implementation 'com.trello.rxlifecycle2:rxlifecycle-android:2.2.2'
    implementation "com.uber.autodispose:autodispose:${versions.autodispose}"
    implementation "com.uber.autodispose:autodispose-android:${versions.autodispose}"
    implementation "com.uber.autodispose:autodispose-lifecycle:${versions.autodispose}"
    implementation "com.uber.autodispose:autodispose-android-archcomponents:${versions.autodispose}"
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.0.0-beta01'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.github.ybq:Android-SpinKit:1.1.0'
    implementation project(path: ':TwigCodesUI')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0-beta01'
    implementation 'com.jakewharton.rxbinding3:rxbinding:3.0.0-alpha2'
}
